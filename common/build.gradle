project.setDescription('Common parts of GeoGebra for various platforms')

buildscript {
	repositories {
    	maven {
    		url 'https://plugins.gradle.org/m2/'
		}
		maven {
			name 'GeoGebra Dev'
			url 'http://dev.geogebra.org/maven2'
		}
	}
	dependencies {
		classpath "ca.coglinc:javacc-gradle-plugin:2.1.0"
		
	}
	configurations.all {
		resolutionStrategy {
			force 'net.java.dev.javacc:javacc:5.0'
		}
	}
}

apply plugin: 'ca.coglinc.javacc'
apply plugin: 'java'
apply plugin: 'findbugs'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
	compile project(':editor-base')
	compile project(':renderer-base')
}

sourceSets {
    nonfree
    gpl
    main {
        java {
            srcDir compileJavacc.outputDirectory
            srcDir compileJavacc.inputDirectory
        }
        resources {
            if (project.hasProperty("usegpl")) {
                srcDirs += gpl.resources.srcDirs
            } else {
                srcDirs += nonfree.resources.srcDirs
            }
        }
    }
}


compileJavacc {
	arguments = [static: 'false']
}

task cleanJavacc(type: Delete) {
	description 'Cleans the javacc generated files.'
	delete compileJavacc
}

test {
     ignoreFailures = true
}

findbugsTask{
     ignoreFailures = true
}
